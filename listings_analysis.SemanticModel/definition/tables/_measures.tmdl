table _measures
	lineageTag: 2821e051-91d3-41ac-8e1b-8b3ca1600053

	measure Listings = ```
			
			VAR _max_date = MAX (dim_dates[Fecha])
			VAR _publicated_current_month = 
			    CALCULATE (
			        DISTINCTCOUNT ( fac_listings[listing_id] ),
			        fac_listings[publication_date] <= _max_date
			    )
			
			RETURN
			_publicated_current_month
			```
		formatString: 0
		displayFolder: KPIs
		lineageTag: d184835a-bd11-4aba-8517-c8558336ac3a

	measure 'Precio de venta promedio' =
			
			CALCULATE(
			    AVERAGE(fac_listings[price]),
			    fac_listings[deactivation_reason] = "Closed"
			)
		displayFolder: KPIs
		lineageTag: 32ae598e-7ea0-4fab-8fa3-7d1170038b96

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Leads = ```
			
			VAR _leads = COUNTROWS(fac_leads)
			
			RETURN
			_leads
			
			```
		formatString: 0
		displayFolder: KPIs
		lineageTag: 6c529afd-5f8f-42e0-813d-5e212a179de7

	measure 'Ratio Leads/Listings' = [Leads] / [Listings]
		formatString: 0.00
		displayFolder: KPIs
		lineageTag: 444ce03c-8aa2-480e-a6de-9cc3429c7bea

	measure Concretadas =
			
			CALCULATE(
			    COUNTROWS(fac_listings),
			    fac_listings[deactivation_reason] = "Closed",
			    USERELATIONSHIP(fac_listings[deactivation_date], dim_dates[Fecha])
			)
		displayFolder: KPIs
		lineageTag: e7bf869f-67ee-42f3-97d4-3d45e5e031d6

	measure _selected_dates =
			
			VAR FechaMin = FIRSTDATE(dim_dates[Fecha])
			VAR FechaMax = LASTDATE(dim_dates[Fecha])
			
			RETURN
			    "Fechas filtradas: "
			        & UNICHAR ( 10 ) & FechaMin & " - " & FechaMax
		isHidden
		displayFolder: _various
		lineageTag: 43775d8b-b70c-49bb-bc25-3481d3988b2f

		changedProperty = IsHidden

	measure _selected_measure_color =
			
			VAR _selected =
			    MAX ( _measure_selector[Parameter] )
			RETURN
			SWITCH (
			    _selected,
			    "Listings", "#D4AF37",
			    "Leads", "#0C4DA2",
			    "Ratio Leads/Listings", "#D57A42",
			    "Concretadas", "#6FCF97",
			    "#000000"
			)
		displayFolder: _various
		lineageTag: 18618eaf-86de-4536-86c3-8967248a022c

	measure 'Edad promedio de cliente' = AVERAGE(dim_customers[age])
		formatString: 0
		displayFolder: KPIs
		lineageTag: 18875b59-7019-4953-924e-13cf62ed1a58

	measure 'Ocupación más común' =
			
			VAR _table =
			    ADDCOLUMNS(
			        FILTER(
			            VALUES ( dim_customers[occupation] ),
			            NOT ISBLANK ( dim_customers[occupation] )
			        ),
			        "__Leads", [Leads]
			    )
			RETURN
			    CONCATENATEX (
			        TOPN ( 1, _table, [__Leads], DESC ),
			        dim_customers[occupation],
			        ", "
			    )
		displayFolder: KPIs
		lineageTag: 5773034b-c1b7-4a34-a66a-afda25d36cc6

	measure 'Leads MoM' =
			
			VAR _previous =
			    CALCULATE(
			        [Leads],
			        PREVIOUSMONTH(dim_dates[Fecha])
			    )
			VAR _current = [Leads]
			VAR _result = (_current - _previous) / _previous
			
			RETURN
			IF(_previous <> 0, _result)
		formatString: 0\ %;-0\ %;0\ %
		displayFolder: KPIs
		lineageTag: b05a2883-d5c4-4f8e-b874-3eeb95d78e9f

	measure 'Listings MoM' =
			
			VAR _previous =
			    CALCULATE(
			        [Listings],
			        PREVIOUSMONTH(dim_dates[Fecha])
			    )
			VAR _current = [Listings]
			VAR _result = (_current - _previous) / _previous
			
			RETURN
			IF(_previous <> 0, _result)
		formatString: 0\ %;-0\ %;0\ %
		displayFolder: KPIs
		lineageTag: 5b561367-524f-4a7c-9062-e3f70d5fedef

	measure 'Ratio MoM' =
			
			VAR _previous =
			    CALCULATE(
			        [Ratio Leads/Listings],
			        PREVIOUSMONTH(dim_dates[Fecha])
			    )
			VAR _current = [Ratio Leads/Listings]
			VAR _result = (_current - _previous) / _previous
			
			RETURN
			IF(_previous <> 0, _result)
		formatString: 0\ %;-0\ %;0\ %
		displayFolder: KPIs
		lineageTag: 7b83f9ca-be63-429b-a8b3-cfd6c08365a0

	column medidas
		dataType: string
		lineageTag: 5055c3b3-546b-4d2a-95b1-69c61ada5ae8
		summarizeBy: none
		sourceColumn: medidas

		annotation SummarizationSetBy = Automatic

	partition _measures = m
		mode: import
		source =
				let
				    Source = #table(
				        {"medidas"},
				        {
				            {"1"}
				        }
				    )
				in
				    Source

	annotation PBI_NavigationStepName = Navigation

	annotation PBI_ResultType = Table

